plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.google.protobuf' version '0.8.19'
}

group 'aliqornan.grpc.example'
version '1.0.0'

ext {
    grpcVersion = '1.51.0'
    grpcKtVersion = '1.3.0'
    protoVersion = '3.21.10'
    junitVersion = '5.8.1'
}

repositories {
    gradlePluginPortal()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation "io.grpc:grpc-kotlin-stub:$grpcKtVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "com.google.protobuf:protobuf-kotlin:$protoVersion"
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java',
                    'build/generated/source/proto/main/grpc'
        }
        kotlin {
            srcDirs 'build/generated/source/proto/main/kotlin',
                    'build/generated/source/proto/main/grpckt'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protoVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpcKtVersion:jdk8@jar"
        }
    }
    generateProtoTasks {
        all().forEach {
            it.plugins {
                grpc {}
                grpckt {}
            }
            it.builtins {
                kotlin {}
            }
        }
    }
}